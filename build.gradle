plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.8'
}

def propertiesFile = file("personal.properties")

if (propertiesFile.exists()) {
    def properties = new Properties()
    propertiesFile.withInputStream { stream -> properties.load(stream)
    }

    properties.each { key, value -> project.ext[key] = value
    }
}

project.ext {
    javaVersion = property('java.version')
    targetFolder = file('target/')
    apiFolder = file('target-api/')
}

allprojects {

    group = 'fr.maxlego08.quests'
    version = property('plugin.version')

    apply {
        plugin 'java'
        plugin 'io.github.goooler.shadow'
    }

    repositories {
        mavenCentral()
        maven {
            name = "jitpack"
            url = 'https://jitpack.io'
        }
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'}
        maven { url = 'https://repo.bg-software.com/repository/api/' }
    }

    dependencies {
        compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
        compileOnly 'me.clip:placeholderapi:2.11.6'
        compileOnly 'com.github.Maxlego08:zMenu-API:c11869b6ef'
        compileOnly 'dev.krakenied:blocktracker:1.0.6'
        compileOnly 'com.bgsoftware:WildStackerAPI:2024.3'
        compileOnly 'com.bgsoftware:SuperiorSkyblockAPI:2024.4'

        compileOnly 'com.mojang:authlib:3.11.50' //only java 16 or newer
        //compileOnly 'com.mojang:authlib:1.6.25'
        implementation "com.github.technicallycoded:FoliaLib:main-SNAPSHOT"

        compileOnly files('libs/zJobs-1.0.0.jar')
        compileOnly files('libs/zshop-3.1.7.jar')

        implementation 'com.github.Maxlego08:Sarah:1.15'
    }

}

dependencies {
    implementation project(':API')
}

shadowJar {
    dependsOn(jar)

    relocate 'com.tcoded.folialib', 'fr.maxlego08.quests.libs.folia'
    relocate 'fr.maxlego08.sarah', 'fr.maxlego08.quests.libs.sarah'

    archiveFileName = rootProject.name + "-" + rootProject.version + ".jar"
    destinationDirectory = rootProject.targetFolder
}

def targetJavaVersion = javaVersion as int
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version, name: rootProject.name]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('**/*.yml') {
        expand props
    }
}

tasks.register('publish') {
    dependsOn ':API:publish'
}

tasks.register('copyFiles', Copy) {
    from "${rootProject.targetFolder}"
    into rootProject.testFolder
}

if (propertiesFile.exists()) {
    shadowJar.finalizedBy('copyFiles')
} else {
    println "No personal.properties file found, skipping copyFiles task"
}

build.finalizedBy(shadowJar)
build.shouldRunAfter subprojects.build

shadowJar.dependsOn subprojects.build
compileJava.dependsOn childProjects.values().shadowJar
